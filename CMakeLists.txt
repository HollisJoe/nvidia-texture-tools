CMAKE_MINIMUM_REQUIRED(VERSION 2.6.0)
PROJECT(NV)
ENABLE_TESTING()

SET(NV_CMAKE_DIR "${NV_SOURCE_DIR}/cmake")
SET(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${NV_CMAKE_DIR}")

IF(WIN32)
	# gnuwin32 paths:
	SET(GNUWIN32_PATH "${NV_SOURCE_DIR}/extern/gnuwin")
	SET(CMAKE_INCLUDE_PATH "${GNUWIN32_PATH}/include")
	IF (NV_SYSTEM_PROCESSOR STREQUAL "AMD64")
	  SET(CMAKE_LIBRARY_PATH ${CMAKE_LIBRARY_PATH} "${GNUWIN32_PATH}/lib/win64")
	ELSE(NV_SYSTEM_PROCESSOR STREQUAL "AMD64")
	  SET(CMAKE_LIBRARY_PATH ${CMAKE_LIBRARY_PATH} "${GNUWIN32_PATH}/lib/win32")
	ENDIF(NV_SYSTEM_PROCESSOR STREQUAL "AMD64")
	
	# Set GLUT path:
	SET(GLUT_ROOT_PATH "${NV_SOURCE_DIR}/extern/glut")

ENDIF(WIN32)

INCLUDE(${NV_CMAKE_DIR}/OptimalOptions.cmake)
MESSAGE(STATUS "Setting optimal options")
MESSAGE(STATUS "  Processor: ${NV_SYSTEM_PROCESSOR}")
MESSAGE(STATUS "  Compiler Flags: ${CMAKE_CXX_FLAGS}")

IF(NVTT_SHARED)
	SET(NVCORE_SHARED TRUE)
	SET(NVMATH_SHARED TRUE)
	SET(NVIMAGE_SHARED TRUE)
ENDIF(NVTT_SHARED)

ADD_SUBDIRECTORY(extern)

ADD_SUBDIRECTORY(src)

INSTALL(FILES 
	NVIDIA_Texture_Tools_LICENSE.txt
	NVIDIA_Texture_Tools_README.txt
	ChangeLog 
	DESTINATION .)

# Add packaging support
INCLUDE(InstallRequiredSystemLibraries)

IF(CMAKE_SYSTEM_NAME STREQUAL "Linux")
	SET(CPACK_GENERATOR "TGZ;DEB")
ENDIF(CMAKE_SYSTEM_NAME STREQUAL "Linux")

SET(CPACK_PACKAGE_NAME "nvidia-texture-tools")
SET(CPACK_PACKAGE_VERSION "2.1.0")
SET(CPACK_PACKAGE_CONTACT "Ignacio Castaño <icastano@nvidia.com>")
SET(CPACK_PACKAGE_VENDOR "NVIDIA Corporation")
SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "GPU-accelerated Texture Tools with support for DirectX 10 texture formats")

SET(CPACK_PACKAGE_ICON "${NV_SOURCE_DIR}/project/vc8/nvcompress/nvidia.ico")

SET(CPACK_PACKAGE_DESCRIPTION_FILE "${NV_SOURCE_DIR}/NVIDIA_Texture_Tools_README.txt")
SET(CPACK_RESOURCE_FILE_LICENSE "${NV_SOURCE_DIR}/NVIDIA_Texture_Tools_LICENSE.txt")

INCLUDE(CPack)
